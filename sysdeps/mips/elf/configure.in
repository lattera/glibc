GLIBC_PROVIDES dnl See aclocal.m4 in the top level source directory.
# Local configure fragment for sysdeps/mips/elf.

# Check for support of thread-local storage handling in assembler and
# linker.
AC_CACHE_CHECK(for MIPS TLS support, libc_cv_mips_tls, [dnl
cat > conftest.s <<\EOF
	.section ".tdata", "awT", %progbits
	.globl foo
foo:	.long	1
	.section ".tbss", "awT", %nobits
	.globl bar
bar:	.skip	4
	.text

	lw	$25, %call16(__tls_get_addr)($28)
	jalr	$25
	addiu	$4, $28, %tlsgd(x) 
EOF
dnl
if AC_TRY_COMMAND(${CC-cc} -c $CFLAGS conftest.s 1>&AS_MESSAGE_LOG_FD); then
  libc_cv_mips_tls=yes
else
  libc_cv_mips_tls=no
fi
rm -f conftest*])
if test $libc_cv_mips_tls = no; then
  AC_MSG_ERROR([the assembler must support TLS])
fi

dnl No MIPS GCC supports accessing static and hidden symbols in an
dnl position independent way.
dnl AC_DEFINE(PI_STATIC_AND_HIDDEN)
